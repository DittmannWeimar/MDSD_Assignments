grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	'program' name=ID
	('external' external+=External)*
	math=MathExp
;

External:
	name=ID '(' (type+=ID (',' type+=ID)*)? ')'
;

ExternalCall:
	external=[External] '(' ((parms+=Exp) (',' parms+=Exp)*)? ')'
;

MathExp:
	variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  
	| {Parenthesis} '(' exp=Exp ')'  
	|  VariableUse 
	| LetBinding
	| ExternalCall
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
