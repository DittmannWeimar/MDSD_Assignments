grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"


MathExp:
	(exps += Math)+
;

Math:
	'var' name=ID '=' exp=Exp
;

Exp:
	PlusMinus
;

PlusMinus returns Exp:
	MultiDiv (({Plus.left=current} '+'| {Minus.left=current} '-') right= MultiDiv)*
;

MultiDiv returns Exp:
	Primary (({Multi.left=current} '*' | {Div.left=current} '/') right= Primary)*
;

Primary:
	Number | Parenthesis | VariableUse | Let
;

Parenthesis:
	'(' exp=Exp ')'
;

Number:
	value=INT
;

VariableUse:
	ref = [Math]
;

Let:
	'let' let=Math 'in' in=Exp 'end'
;

